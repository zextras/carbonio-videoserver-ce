pkgname="carbonio-videoserver-confs-ce"
pkgver="1.1.4"
pkgrel="2"
pkgdesc="An open source, general purpose, WebRTC server (configs only)"
arch=('x86_64')
maintainer="Zextras <packages@zextras.com"
url="https://zextras.com"
depends__apt=(
  "carbonio-curl"
  "carbonio-ffmpeg"
  "carbonio-libnice"
  "carbonio-libopus"
  "carbonio-librabbitmq-c"
  "carbonio-libsrtp"
  "carbonio-libusrsctp"
  "carbonio-libwebsockets"
  "carbonio-openssl"
  "cdebconf"
  "debconf"
  "dialog"
  "libc6"
  "libconfig9"
  "libglib2.0-0"
  "libjansson4"
  "libmicrohttpd12"
  "libogg0"
  "libterm-readline-gnu-perl"
  "zlib1g"
)
depends__yum=(
  "carbonio-curl"
  "carbonio-ffmpeg"
  "carbonio-libnice"
  "carbonio-libopus"
  "carbonio-librabbitmq-c"
  "carbonio-libsrtp"
  "carbonio-libusrsctp"
  "carbonio-libwebsockets"
  "carbonio-openssl"
  "jansson"
  "libconfig"
  "libmicrohttpd"
  "libogg"
  "zlib"
)
makedepends__apt=(
  "carbonio-curl"
  "carbonio-openssl"
  "gengetopt"
  "libconfig-dev"
  "libjansson-dev"
  "libmicrohttpd-dev"
  "libogg-dev"
  "zlib1g-dev"
)
makedepends__yum=(
  "carbonio-curl"
  "carbonio-openssl"
  "gengetopt"
  "jansson-devel"
  "libconfig-devel"
  "libmicrohttpd-devel"
  "libogg-devel"
  "zlib-devel"
)
section="comm"
priority="important"
backup=(
  "etc/janus/janus.jcfg"
)
source=(
  "https://github.com/meetecho/janus-gateway/archive/refs/tags/v${pkgver}.tar.gz"
  "janus.jcfg.patch"
)
sha256sums=('292397ee925b957fec3e946b8b40d559fcdfc80d171ab9618f10df0e1ebe800f'
  '667552cdec3a848fb1f136819d8116e73f586d28411c468abf08b288b9eb2756')

build() {
  cd "${srcdir}/janus-gateway-${pkgver}"
  patch -Np1 -i ../janus.jcfg.patch
  export CFLAGS="-I/opt/zextras/common/include"
  export LDFLAGS="-Wl,-rpath,/opt/zextras/common/lib \
    -L/opt/zextras/common/lib \
    -L/usr/lib/x86_64-linux-gnu \
    -L/usr/lib"
  export PKG_CONFIG_PATH="/opt/zextras/common/lib/pkgconfig"

  ./autogen.sh
  ./configure \
    --disable-all-handlers \
    --disable-all-plugins \
    --disable-all-transports \
    --disable-docs \
    --disable-json-logger \
    --disable-turn-rest-api \
    --enable-plugin-audiobridge \
    --enable-plugin-videoroom \
    --enable-post-processing \
    --enable-rabbitmq \
    --enable-rabbitmq-event-handler \
    --enable-rest \
    --enable-websockets \
    --enable-websockets-event-handler \
    --prefix=/opt/zextras/common \
    --sysconfdir /etc
  make -j8
}

package() {
  cd "${srcdir}/janus-gateway-${pkgver}"
  make DESTDIR="${pkgdir}" install configs
  rm -rf "${pkgdir}"/opt/
}
